
Функция ПолучитьЗначениеНастройки(знач ИмяНастройки, знач ПользовательСсылка) Экспорт
	
	Если НЕ ТипЗнч(ПользовательСсылка) = Тип("СправочникСсылка.Пользователи") Тогда
		ПользовательСсылка = Справочники.Пользователи.ПустаяСсылка();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИмяНастройки"	, ИмяНастройки);
	Запрос.УстановитьПараметр("Пользователь"	, ПользовательСсылка);
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	БЗ_НастройкиБазыЗнаний.ИмяНастройки,
	|	БЗ_НастройкиБазыЗнаний.Пользователь,
	|	БЗ_НастройкиБазыЗнаний.ЗначениеСсылка,
	|	БЗ_НастройкиБазыЗнаний.ЗначениеЧисло,
	|	БЗ_НастройкиБазыЗнаний.ЗначениеСтрока,
	|	БЗ_НастройкиБазыЗнаний.ЗначениеБулево,
	|	БЗ_НастройкиБазыЗнаний.ИмяРесурса
	|ИЗ
	|	РегистрСведений.БЗ_НастройкиБазыЗнаний КАК БЗ_НастройкиБазыЗнаний
	|ГДЕ
	|	БЗ_НастройкиБазыЗнаний.ИмяНастройки = &ИмяНастройки
	|	И БЗ_НастройкиБазыЗнаний.Пользователь = &Пользователь";
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	
	Возврат Выборка[Выборка.ИмяРесурса];
	
КонецФункции

