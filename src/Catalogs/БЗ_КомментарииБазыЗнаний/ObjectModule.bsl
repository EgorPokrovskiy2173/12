Перем РодительСсылка Экспорт;

////////////////////////////////////////////////////////////////////////////////
// ПРОЧИЕ МЕТОДЫ

Функция ПолучитьРодителяКомментария(КомментарийСсылка)
	
	Возврат Справочники.БЗ_КомментарииБазыЗнаний.ПолучитьЗначениеРодитель(КомментарийСсылка);
	
КонецФункции

Функция ПолучитьПользователейДляОповещения()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ЭтотОбъект.Ссылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	БЗ_КомментарииБазыЗнаний.Владелец.Ответственный КАК Пользователь,
	|	1 КАК ВидОповещения
	|ИЗ
	|	Справочник.БЗ_КомментарииБазыЗнаний КАК БЗ_КомментарииБазыЗнаний
	|ГДЕ
	|	БЗ_КомментарииБазыЗнаний.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	БЗ_СвязьКомментариевБазыЗнаний.Родитель.Автор,
	|	2
	|ИЗ
	|	РегистрСведений.БЗ_СвязьКомментариевБазыЗнаний КАК БЗ_СвязьКомментариевБазыЗнаний
	|ГДЕ
	|	БЗ_СвязьКомментариевБазыЗнаний.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВидОповещения";
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Процедура ЗаписатьОповещениеПользователей()
	
	ЭтоНовый = (ЭтотОбъект.ДополнительныеСвойства.Свойство("ЭтоНовый") И ЭтотОбъект.ДополнительныеСвойства.ЭтоНовый);
	Если НЕ ЭтоНовый Тогда
		Возврат;
	КонецЕсли;
	
	Получатели = ПолучитьПользователейДляОповещения();
	
	Если Получатели.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Обработанные = Новый Массив;
	НаборЗаписей = РегистрыСведений.БЗ_ОповещенияБазыЗнаний.СоздатьНаборЗаписей();
	Для Каждого СтрокаТаблицы Из Получатели Цикл
		Если Обработанные.Найти(СтрокаТаблицы.Пользователь) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если СтрокаТаблицы.ВидОповещения = 1 Тогда
			СтрОписание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Получен новый комментарий к статье %1 
				|от пользователя %2'"),
				Строка(ЭтотОбъект.Владелец),
				Строка(ЭтотОбъект.Автор));
		ИначеЕсли СтрокаТаблицы.ВидОповещения = 2 Тогда
			СтрОписание = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru='Новый ответ от пользователя %1'"),
				Строка(ЭтотОбъект.Автор));
		КонецЕсли;
		
		ЗаписьРегистра = НаборЗаписей.Добавить();
		ЗаписьРегистра.Пользователь		= СтрокаТаблицы.Пользователь;
		ЗаписьРегистра.ВидОповещения	= 1; // см. подсказку к измерению регистра
		ЗаписьРегистра.Идентификатор	= Новый УникальныйИдентификатор;
		ЗаписьРегистра.Объект			= ЭтотОбъект.Ссылка;
		ЗаписьРегистра.ВидСобытия		= СтрокаТаблицы.ВидОповещения; // см. модуль менеджера регистра
		ЗаписьРегистра.Описание			= СтрОписание;
		
		Обработанные.Добавить(СтрокаТаблицы.Пользователь);
	КонецЦикла;
	НаборЗаписей.Записать(Истина);
	
КонецПроцедуры

Процедура ОбновитьСтатистикуКомментариев()
	
	ПараметрыВыполнения = Новый Структура("Статья", ЭтотОбъект.Владелец);
	
	МассивПараметров = Новый Массив;
	МассивПараметров.Добавить(ПараметрыВыполнения);
	
	ФоновыеЗадания.Выполнить(
		"БЗ_БазаЗнаний.ОбновитьКоличествоКомментариев",
		МассивПараметров, ,
		"Обновление количества комментариев");
	
КонецПроцедуры

// ТипНовости: 1 - создание, 2 - изменение, 3 - удаление
Функция ДобавитьНовостьБазыЗнаний(ТипНовости)
	
	ПараметрыСсылкиОтв	= Новый Структура("id", Строка(ЭтотОбъект.Автор.УникальныйИдентификатор()));
	АдресСсылкиОтв		= БЗ_БазаЗнанийAPIКлиентСервер.КонструкторСсылки_page("user", ПараметрыСсылкиОтв);
	
	ПараметрыСсылкиСтат	= Новый Структура("id", Строка(ЭтотОбъект.Владелец.УникальныйИдентификатор()));
	АдресСсылкиСтат		= БЗ_БазаЗнанийAPIКлиентСервер.КонструкторСсылки_page("article", ПараметрыСсылкиСтат);
	
	ПараметрыСсылкиКомм	= ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(ПараметрыСсылкиСтат);
	ПараметрыСсылкиКомм.Вставить("section", "comment_" + Строка(ЭтотОбъект.Ссылка.УникальныйИдентификатор()));
	АдресСсылкиКом		= БЗ_БазаЗнанийAPIКлиентСервер.КонструкторСсылки_page("article", ПараметрыСсылкиКомм);
	
	Если ТипНовости = 1 Тогда
		ВидНовости	= Перечисления.БЗ_ВидыНовостейБазыЗнаний.Создание;
		Заголовок	= "Новый комментарий в статье '%3' от пользователя %4";
		НовостьHTML	= "<div>%1 в %2 добавлен новый комментарий к статье <a href='" + АдресСсылкиСтат + "'>%3</a>.
		|	<br>Автор: <a href='" + АдресСсылкиОтв + "'>%4</a>
		|	<br>Текст: %5.
		|	<br><a href='" + АдресСсылкиКом + "'>Перейти</a>
		|</div>";
	ИначеЕсли ТипНовости = 2 Тогда
		ВидНовости	= Перечисления.БЗ_ВидыНовостейБазыЗнаний.Изменение;
		Заголовок	= "Изменен комментарий в статье '%3' от пользователя %4";
		НовостьHTML	= "<div>%1 в %2 изменен комментарий в статье <a href='" + АдресСсылкиСтат + "'>%3</a>.
		|	<br>Автор: <a href='" + АдресСсылкиОтв + "'>%4</a>
		|	<br>Текст: %5.
		|	<br><a href='" + АдресСсылкиКом + "'>Перейти</a>
		|</div>";
	ИначеЕсли ТипНовости = 3 Тогда
		ВидНовости	= Перечисления.БЗ_ВидыНовостейБазыЗнаний.Удаление;
		Заголовок	= "Удален комментарий в статье '%3' от пользователя '%4'";
		НовостьHTML	= "<div>%1 в %2 удален комментарий в статье <a href='" + АдресСсылкиСтат + "'>%3</a>.
		|	<br>Автор: <a href='" + АдресСсылкиОтв + "'>%4</a>
		|	<br>Текст: %5.
		|</div>";
	КонецЕсли;
	
	ДатаНовости = ТекущаяДатаСеанса();
	
	МассивПараметров = Новый Массив;
	МассивПараметров.Добавить(Формат(ДатаНовости, "ДЛФ=DD"));
	МассивПараметров.Добавить(Формат(ДатаНовости, "ДФ=HH:mm"));
	МассивПараметров.Добавить(Строка(ЭтотОбъект.Владелец));
	МассивПараметров.Добавить(Строка(ЭтотОбъект.Автор));
	МассивПараметров.Добавить(ЭтотОбъект.Текст);
	
	Заголовок	= СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтрокуИзМассива(Заголовок, МассивПараметров);
	НовостьHTML	= СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтрокуИзМассива(НовостьHTML, МассивПараметров);
	
	Получатели	= Новый Массив;
	Если ЗначениеЗаполнено(РодительСсылка) Тогда
		ПользовательАвтор = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(РодительСсылка, "Автор");
		Если ПользовательАвтор <> Пользователи.ТекущийПользователь() Тогда
			Получатели.Добавить(ПользовательАвтор);
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыНовости = Новый Структура;
	ПараметрыНовости.Вставить("Автор"				, ЭтотОбъект.Автор);
	ПараметрыНовости.Вставить("Заголовок"			, Заголовок);
	ПараметрыНовости.Вставить("ТекстHTML"			, НовостьHTML);
	ПараметрыНовости.Вставить("ОбъектБазыЗнаний"	, ЭтотОбъект.Ссылка);
	ПараметрыНовости.Вставить("Важность"			, 2);
	ПараметрыНовости.Вставить("Получатели"			, Получатели);
	ПараметрыНовости.Вставить("ВидНовости"			, ВидНовости);
	
	БЗ_БазаЗнанийВызовСервера.ДобавитьНовость(ПараметрыНовости);
	
КонецФункции

Функция УдалитьНовостиБазыЗнаний()
	
	МассивСсылок = БЗ_БазаЗнаний.ПолучитьНовостиПоОбъекту(ЭтотОбъект.Ссылка);
	
	ПараметрыВыполнения = Новый Структура;
	ПараметрыВыполнения.Вставить("Ссылки", МассивСсылок);
	
	МассивПараметров = Новый Массив;
	МассивПараметров.Добавить(ПараметрыВыполнения);
	
	ФоновыеЗадания.Выполнить(
		"БЗ_БазаЗнаний.УдалитьНовости",
		МассивПараметров,
		,
		"Удаление новостей базы знаний по объекту");
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТКА СОБЫТИЙ МОДУЛЯ

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ТекстИзменился = ЭтотОбъект.ЭтоНовый();
	Если НЕ ТекстИзменился Тогда
		ТекущийТекст	= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЭтотОбъект.Ссылка, "Текст");
		ТекстИзменился	= (ТекущийТекст <> ЭтотОбъект.Текст);
	КонецЕсли;
	
	ЭтотОбъект.ДополнительныеСвойства.Вставить("ЭтоНовый"		, ЭтотОбъект.ЭтоНовый());
	ЭтотОбъект.ДополнительныеСвойства.Вставить("ТекстИзменился"	, ТекстИзменился);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоНовый		= (ЭтотОбъект.ДополнительныеСвойства.Свойство("ЭтоНовый") И ЭтотОбъект.ДополнительныеСвойства.ЭтоНовый);
	ТекстИзменился	= (ЭтотОбъект.ДополнительныеСвойства.Свойство("ТекстИзменился") И ЭтотОбъект.ДополнительныеСвойства.ТекстИзменился);
	
	Если ЭтоНовый ИЛИ ТекстИзменился Тогда
		ДобавитьНовостьБазыЗнаний(?(ЭтоНовый, 1, 2));
	КонецЕсли;
	
	ТекущийРодитель = ПолучитьРодителяКомментария(ЭтотОбъект.Ссылка);
	Если ТекущийРодитель <> РодительСсылка И НЕ ТекущийРодитель.Пустая() Тогда
		РегистрыСведений.БЗ_СвязьКомментариевБазыЗнаний.ОчиститьСвязь(ТекущийРодитель, ЭтотОбъект.Ссылка);
	КонецЕсли;
	Если НЕ РодительСсылка.Пустая() Тогда
		РегистрыСведений.БЗ_СвязьКомментариевБазыЗнаний.УстановитьСвязь(РодительСсылка, ЭтотОбъект.Ссылка);
	КонецЕсли;
	
	Если ЭтоНовый Тогда
		ОбновитьСтатистикуКомментариев();
	КонецЕсли;
	
	ЗаписатьОповещениеПользователей();
	
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	ДобавитьНовостьБазыЗнаний(3);
	УдалитьНовостиБазыЗнаний();
	
	РегистрыСведений.БЗ_СвязьКомментариевБазыЗнаний.ОчиститьСвязь(РодительСсылка, ЭтотОбъект.Ссылка);
КонецПроцедуры

РодительСсылка = ПолучитьРодителяКомментария(ЭтотОбъект.Ссылка);