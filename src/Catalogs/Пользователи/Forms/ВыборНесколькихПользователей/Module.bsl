
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("Выбранные") Тогда
		МассивВыбранные = Параметры.Выбранные;
	Иначе 
		МассивВыбранные = Новый Массив;
	КонецЕсли;
	
	Если Параметры.Свойство("Запрещенные") Тогда
		МассивЗапрещенные = Параметры.Запрещенные;
	Иначе 
		МассивЗапрещенные = Новый Массив;
	КонецЕсли;
	
	ЗаполнитьСписокПользователей(МассивВыбранные, МассивЗапрещенные);
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокПользователей(МассивВыбранные, Запрещенные)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ #Вставка1# 				
		|	Пользователи.Ссылка КАК Ссылка,
		|	Пользователи.Ссылка В (&Выбранные) КАК Выбран,
		|	Пользователи.Ссылка В (&Запрещенные) КАК ЗапретИзменения
		|ИЗ
		|	Справочник.Пользователи КАК Пользователи
		|ГДЕ
		|	НЕ Пользователи.Недействителен
		|	И НЕ Пользователи.ПометкаУдаления #Вставка2#  	
		|
		|УПОРЯДОЧИТЬ ПО #Вставка3# 			
		|	Пользователи.Наименование #Вставка4#"; 
	
	Запрос.УстановитьПараметр("Выбранные", МассивВыбранные);
	Запрос.УстановитьПараметр("Запрещенные", Запрещенные);
	
	Если Метаданные.Справочники.Найти("Подразделения") <> Неопределено Или Метаданные.Справочники.Найти("ПодразделенияОрганизаций") <> Неопределено Тогда
		ТекстЗапроса = Запрос.Текст;                                              
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#Вставка1#", Символы.ПС + "	Пользователи.Подразделение КАК Подразделение,");
		Если Метаданные.Справочники.Пользователи.Реквизиты.Подразделение.Тип = Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(10)) Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#Вставка2#", Символы.ПС + "");
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#Вставка3#", Символы.ПС + "	Пользователи.Подразделение,");
		Иначе
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#Вставка2#", Символы.ПС + "	И НЕ Пользователи.Подразделение.ПометкаУдаления");
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#Вставка3#", Символы.ПС + "	Пользователи.Подразделение.Наименование,");
		КонецЕсли;
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#Вставка4#", Символы.ПС + "ИТОГИ" + Символы.ПС + "	ПО Подразделение ИЕРАРХИЯ"); 
		Запрос.Текст = ТекстЗапроса; 
		
		РезультатЗапроса = Запрос.Выполнить();
		ВыборкаПодразделение = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
		
		ЭлементыДерева = Список.ПолучитьЭлементы();
		ЭлементДерева = ЭлементыДерева.Добавить();
		ЭлементДерева.ИндексКартинки = 2;
		ЭлементДерева.Пользователь = "Все пользователи";
		
		ПодчинённыеВсеВыбраны = Истина;
		СоздатьЭлементыДереваРекурсивно(ЭлементДерева.ПолучитьЭлементы(), ВыборкаПодразделение, ПодчинённыеВсеВыбраны);
		ЭлементДерева.Выбран = ПодчинённыеВсеВыбраны;
	Иначе
		ТекстЗапроса = Запрос.Текст;                                              
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#Вставка1#", "");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#Вставка2#", "");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#Вставка3#", "");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "#Вставка4#", ""); 
		Запрос.Текст = ТекстЗапроса;  
		
		РезультатЗапроса = Запрос.Выполнить();
		Выборка = РезультатЗапроса.Выбрать();
		
		ЭлементыДерева = Список.ПолучитьЭлементы();
		ЭлементДерева = ЭлементыДерева.Добавить();
		ЭлементДерева.ИндексКартинки = 2;
		ЭлементДерева.Пользователь = "Все пользователи";
		
		ПодчинённыеВсеВыбраны = Истина;
		СоздатьЭлементыДереваРекурсивно(ЭлементДерева.ПолучитьЭлементы(), Выборка, ПодчинённыеВсеВыбраны);
		ЭлементДерева.Выбран = ПодчинённыеВсеВыбраны;
	КонецЕсли;
	
КонецПроцедуры

Процедура СоздатьЭлементыДереваРекурсивно(ЭлементыДерева, Выборка, ВсеВыбраны = Истина, ВсеЗапретИзменения = Истина, Подразделение = Неопределено)
	
	Пока Выборка.Следующий() Цикл
		Если Выборка.Ссылка <> NULL Тогда
			ЭлементДерева = ЭлементыДерева.Добавить();
			ЭлементДерева.Пользователь = Выборка.Ссылка;
			ЭлементДерева.Выбран = Выборка.Выбран;
			ЭлементДерева.ЗапретИзменения = Выборка.ЗапретИзменения;
			ЭлементДерева.ИндексКартинки = 3;
			
			ВсеВыбраны = Мин(ВсеВыбраны, Выборка.Выбран);
			ВсеЗапретИзменения = Мин(ВсеЗапретИзменения, Выборка.ЗапретИзменения);
		ИначеЕсли Подразделение <> Неопределено И Выборка.Подразделение = Подразделение Тогда
			ВыборкаПодразделение = Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			СоздатьЭлементыДереваРекурсивно(ЭлементыДерева, ВыборкаПодразделение, ВсеВыбраны, ВсеЗапретИзменения, Подразделение);
		Иначе
			ЭлементДерева = ЭлементыДерева.Добавить();
			ЭлементДерева.Пользователь = Выборка.Подразделение;
			ЭлементДерева.ИндексКартинки = 2;
			
			Если Выборка.ТипЗаписи() = ТипЗаписиЗапроса.ИтогПоИерархии Тогда
				ВыборкаПодразделение = Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией, "Подразделение");
			ИначеЕсли Выборка.ТипЗаписи() = ТипЗаписиЗапроса.ИтогПоГруппировке Тогда
				ВыборкаПодразделение = Выборка.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			КонецЕсли; 
			
			ПодчинённыеВсеВыбраны = Истина;
			ПодчинённыеВсеЗапрещены = Истина;
			СоздатьЭлементыДереваРекурсивно(ЭлементДерева.ПолучитьЭлементы(), ВыборкаПодразделение, ПодчинённыеВсеВыбраны, ПодчинённыеВсеЗапрещены, Выборка.Подразделение);
			ЭлементДерева.Выбран = ПодчинённыеВсеВыбраны;
			ЭлементДерева.ЗапретИзменения = ПодчинённыеВсеЗапрещены;
			
			ВсеВыбраны = Мин(ВсеВыбраны, ЭлементДерева.Выбран);
			ВсеЗапретИзменения = Мин(ВсеЗапретИзменения, ЭлементДерева.ЗапретИзменения);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыборПриИзменении(Элемент)
	ТекДанные = Элементы.Список.ТекущиеДанные;
	Если ТекДанные <> Неопределено Тогда
		ЭлементыДерева = ТекДанные.ПолучитьЭлементы();                                           
		ОтметитьЭлементыРекурсивно(ЭлементыДерева, ТекДанные.Выбран);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОтметитьЭлементыРекурсивно(ЭлементыДерева, Выбран)
	Если ЗначениеЗаполнено(ЭлементыДерева) Тогда
		Для каждого ЭлементДерева Из ЭлементыДерева Цикл
			Если НЕ ЭлементДерева.ЗапретИзменения Тогда	
				ЭлементДерева.Выбран = Выбран;
				ОтметитьЭлементыРекурсивно(ЭлементДерева.ПолучитьЭлементы(), Выбран);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗакончитьПодбор(Команда)
	
	МассивРезультат = Новый Массив;
	ПолучитьВыбранныеРекурсивно(Список.ПолучитьЭлементы(), МассивРезультат);
	
	Закрыть(МассивРезультат);
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьВыбранныеРекурсивно(ЭлементыДерева, МассивРезультат)
	
	Для каждого ЭлементДерева Из ЭлементыДерева Цикл
		Если НЕ ЭлементДерева.ЗапретИзменения Тогда
			Если ТипЗнч(ЭлементДерева.Пользователь) = Тип("СправочникСсылка.Пользователи")	И 
					ЭлементДерева.Выбран													И 
					МассивРезультат.Найти(ЭлементДерева.Пользователь) = Неопределено				Тогда
					
				МассивРезультат.Добавить(ЭлементДерева.Пользователь);
			Иначе
				ПолучитьВыбранныеРекурсивно(ЭлементДерева.ПолучитьЭлементы(), МассивРезультат);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры
